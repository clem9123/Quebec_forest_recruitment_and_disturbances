model{

    #----------------------------------------#
    #            MODEL PRESENCE              #
    #----------------------------------------#

    # Likelihood
    #-------------

    for (i in 1:N){
    PRESENCE[i] ~ dbern(p[i])

    logit(p[i]) <-
        pa_intercept[IS_SPECIES[i] + 1] +
        # soil
        pa_epmatorg * EPMATORG[i] + pa_ph * PH[i] + pa_soil[SOIL[i]] +
        # climate
        pa_tmean * TMEAN[i] + pa_tmean2 * pow(TMEAN[i],2) +
        pa_cmi * CMI[i] + pa_cmi2 * pow(CMI[i],2) +
        # biotic 
        pa_ba * BA[i] +
        # perturbation
        pa_l[CL_L[i]] +
        pa_pl[CL_PL[i]] +
        pa_lpr[CL_LPR[i]] +
        pa_b[CL_B[i]] +
        pa_o[CL_O[i]]
    }

    # Priors
    #--------

    # soil
    pa_epmatorg ~ dnorm(0, 5)
    pa_ph ~ dnorm(0, 5)
    pa_soil[1] <- 0
    for(i in 2:10){
        pa_soil[i] ~ dnorm(0, 5)
    }
    # climate
    pa_tmean ~ dnorm(0, 5)
    pa_cmi ~ dnorm(0, 5)
    pa_tmean2 ~ dnorm(0, 5)
    pa_cmi2 ~ dnorm(0, 5)
    # biotic
    pa_ba ~ dnorm(0, 5)
    pa_intercept[1] ~ dnorm(0, 5)
    pa_intercept[2] ~ dnorm(0, 5)
    pa_sp ~ dnorm(0, 5)
    # perturbation
    # classes
    for (i in 2:4){
        pa_lpr[i] ~ dnorm(0, 5)
        pa_o[i] ~ dnorm(0, 5)
    }
    #
    for (i in 2:6){
        pa_l[i] ~ dnorm(0, 5)
    }
    for (i in 2:5){
        pa_pl[i] ~ dnorm(0, 5)
    }
    for (i in 2:7){
        pa_b[i] ~ dnorm(0, 5)
    }
    pa_l[1] <- 0
    pa_pl[1] <- 0
    pa_lpr[1] <- 0
    pa_b[1] <- 0
    pa_o[1] <- 0

    #----------------------------------------#
    #            MODEL DENSITE               #
    #----------------------------------------#

    # Pareil que pour la prÃ©sence, mais avec une log et tout les parametre en nb_X

    # Likelihood
    #-------------

    for (i in 1:N){
    DENSITE[i] ~ dpois(lambda[i] * PRESENCE[i])

    log(lambda[i]) <-
        nb_intercept[IS_SPECIES[i] + 1] +
        # soil
        nb_epmatorg * EPMATORG[i] + nb_ph * PH[i] + nb_soil[SOIL[i]] +
        # climate
        nb_tmean * TMEAN[i] + nb_tmean2 * pow(TMEAN[i],2) +
        nb_cmi * CMI[i] + nb_cmi2 * pow(CMI[i],2) +
        # biotic
        nb_ba * BA[i] +
        # perturbation
        nb_l[CL_L[i]] +
        nb_pl[CL_PL[i]] +
        nb_lpr[CL_LPR[i]] +
        nb_b[CL_B[i]] +
        nb_o[CL_O[i]]
    }

    # Priors
    #--------

    # soil
    nb_epmatorg ~ dnorm(0, 5)
    nb_ph ~ dnorm(0, 5)
    nb_soil[1] <- 0
    for(i in 2:10){
        nb_soil[i] ~ dnorm(0, 5)
    }
    # climate
    nb_tmean ~ dnorm(0, 5)
    nb_cmi ~ dnorm(0, 5)
    nb_tmean2 ~ dnorm(0, 5)
    nb_cmi2 ~ dnorm(0, 5)
    # biotic
    nb_ba ~ dnorm(0, 5)
    nb_intercept[1] ~ dnorm(0, 5)
    nb_intercept[2] ~ dnorm(0, 5)
    nb_sp ~ dnorm(0, 5)
    # perturbation
    # classes
    for (i in 2:4){
        nb_lpr[i] ~ dnorm(0, 5)
        nb_o[i] ~ dnorm(0, 5)
    }
    #
    for (i in 2:6){
        nb_l[i] ~ dnorm(0, 5)
    }
    for (i in 2:5){
        nb_pl[i] ~ dnorm(0, 5)
    }
    for (i in 2:7){
        nb_b[i] ~ dnorm(0, 5)
    }
    nb_l[1] <- 0
    nb_pl[1] <- 0
    nb_lpr[1] <- 0
    nb_b[1] <- 0
    nb_o[1] <- 0
}