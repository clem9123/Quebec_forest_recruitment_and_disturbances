model{

    #------------------------------------------#
    #     MODEL PRESENCE CONTINUOUS TIME       #
    #------------------------------------------#

    # Likelihood
    #-------------

    for (i in 1:N){
    PRESENCE[i] ~ dbern(p[i])

    logit(p[i]) <-
        pa_intercept[IS_SPECIES[i] + 1] +
        # soil
        pa_epmatorg * EPMATORG[i] + pa_ph * PH[i] +
        # climate
        pa_tmean * TMEAN[i] + pa_tmean2 * pow(TMEAN[i],2) +
        pa_cmi * CMI[i] + pa_cmi2 * pow(CMI[i],2) +
        # basal area
        pa_ba * BA[i] +
        # perturbation
        pa_eff_l * exp(-(pow(ifelse(TS_L[i]==0, 200, TS_L[i]) - pa_peak_l,2))/pa_var_l) + 
        pa_eff_pl * exp(-(pow(ifelse(TS_PL[i]==0, 200, TS_PL[i]) - pa_peak_pl,2))/ pa_var_pl) + 
        pa_eff_lpr * exp(-(pow(ifelse(TS_LPR[i]==0, 200, TS_LPR[i]) - pa_peak_lpr,2))/pa_var_lpr) + 
        pa_eff_b * exp(-(pow(ifelse(TS_B[i]==0, 200, TS_B[i]) - pa_peak_b,2))/pa_var_b) + 
        pa_eff_o * exp(-(pow(ifelse(TS_O[i]==0, 200, TS_O[i]) - pa_peak_o,2))/pa_var_o)
    }

    # Priors
    #--------

    # soil
    pa_epmatorg ~ dnorm(0, 5)
    pa_ph ~ dnorm(0, 5)
    # climate
    pa_tmean ~ dnorm(0, 5)
    pa_cmi ~ dnorm(0, 5)
    pa_tmean2 ~ dnorm(0, 5)
    pa_cmi2 ~ dnorm(0, 5)
    # biotic
    pa_ba ~ dnorm(0, 5)
    pa_intercept[1] ~ dnorm(0, 5)
    pa_intercept[2] ~ dnorm(0, 5)
    # perturbations
    pa_eff_l ~ dnorm(0, 5)
    pa_eff_pl ~ dnorm(0, 5)
    pa_eff_lpr ~ dnorm(0, 5)
    pa_eff_b ~ dnorm(0, 5)
    pa_eff_o ~ dnorm(0, 5)
    pa_peak_l ~ dchisqr(20)
    pa_peak_pl ~ dchisqr(20)
    pa_peak_lpr ~ dchisqr(20)
    pa_peak_b ~ dchisqr(20)
    pa_peak_o ~ dchisqr(20)
    pa_var_l ~ dchisqr(10)
    pa_var_pl ~ dchisqr(10)
    pa_var_lpr ~ dchisqr(10)
    pa_var_b ~ dchisqr(10)
    pa_var_o ~ dchisqr(10)

    
    #----------------------------------------#
    #            MODEL DENSITE               #
    #----------------------------------------#

    # Pareil que pour la prÃ©sence, mais avec une log et tout les parametre en nb_X

    # Likelihood
    #-------------

    for (i in 1:N){
    DENSITE[i] ~ dpois(lambda[i] * PRESENCE[i])

    log(lambda[i]) <-
        nb_intercept[IS_SPECIES[i] + 1] +
        # soil
        nb_epmatorg * EPMATORG[i] + nb_ph * PH[i] +
        # climate
        nb_tmean * TMEAN[i] + nb_tmean2 * pow(TMEAN[i],2) +
        nb_cmi * CMI[i] + nb_cmi2 * pow(CMI[i],2) +
        # biotic
        nb_ba * BA[i] +
        # perturbations
        nb_eff_l * exp(-(pow(ifelse(TS_L[i]==0, 200, TS_L[i]) - nb_peak_l,2))/nb_var_l) + 
        nb_eff_pl * exp(-(pow(ifelse(TS_PL[i]==0, 200, TS_PL[i]) - nb_peak_pl,2))/nb_var_pl) + 
        nb_eff_lpr * exp(-(pow(ifelse(TS_LPR[i]==0, 200, TS_LPR[i]) - nb_peak_lpr,2))/nb_var_lpr) + 
        nb_eff_b * exp(-(pow(ifelse(TS_B[i]==0, 200, TS_B[i]) - nb_peak_b,2))/nb_var_b) + 
        nb_eff_o * exp(-(pow(ifelse(TS_O[i]==0, 200, TS_O[i]) - nb_peak_o,2))/nb_var_o)
    }

    # Priors
    #--------
    # soil
    nb_epmatorg ~ dnorm(0, 5)
    nb_ph ~ dnorm(0, 5)
    # climate
    nb_tmean ~ dnorm(0, 5)
    nb_cmi ~ dnorm(0, 5)
    nb_tmean2 ~ dnorm(0, 5)
    nb_cmi2 ~ dnorm(0, 5)
    # biotic
    nb_ba ~ dnorm(0, 5)
    nb_intercept[1] ~ dnorm(0, 5)
    nb_intercept[2] ~ dnorm(0, 5)
    # perturbation
    nb_eff_l ~ dnorm(0, 5)
    nb_eff_pl ~ dnorm(0, 5)
    nb_eff_lpr ~ dnorm(0, 5)
    nb_eff_b ~ dnorm(0, 5)
    nb_eff_o ~ dnorm(0, 5)
    nb_peak_l ~ dchisqr(20)
    nb_peak_pl ~ dchisqr(20)
    nb_peak_lpr ~ dchisqr(20)
    nb_peak_b ~ dchisqr(20)
    nb_peak_o ~ dchisqr(20)
    nb_var_l ~ dchisqr(10)
    nb_var_pl ~ dchisqr(10)
    nb_var_lpr ~ dchisqr(10)
    nb_var_b ~ dchisqr(10)
    nb_var_o ~ dchisqr(10)
}